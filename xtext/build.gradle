plugins{
    id 'library.deps-conventions'
    id 'library.java-conventions'

    // The following plugin is used to build the OSGI bundle
    id 'biz.aQute.bnd.builder' version '6.4.0'
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'src/main/src-gen']
        }
    }
}

jar {
    archiveBaseName.set('xtext')
    archiveVersion.set('1.0.0.0')
    archiveFileName.set("${archiveBaseName.get()}_${archiveVersion.get()}.jar")
}

configurations {
    eclipseLibs
    implementation.extendsFrom(eclipseLibs)
}

dependencies {
    //module dependencies
    implementation project(':third-party')
//    implementation project(':wrims')
//    implementation project(':hecdssvue')

    //additional dependencies
    implementation libs.hec.monolith
    implementation libs.hec.nucleus.metadata
    implementation libs.dssgui
    implementation libs.guice

    implementation 'commons-cli:commons-cli:1.4'
    implementation 'log4j:log4j:1.2.17'
    implementation 'com.ibm.icu:icu4j:71.1'

    implementation(":antlr-generator-3.2.0")
    implementation(":antlr-runtime-3.2")

    //eclipse libs
    eclipseLibs fileTree(dir: '../eclipse-luna-libs', include: ['*.jar'])
}

configurations.configureEach {
    exclude group: "javax.media", module: "jai_core"
    exclude group: "javax.media", module: "jai_codec"
    exclude group: "com.sun.media", module: "jai_imageio"
}

task runMwe2(type: JavaExec) {
    main = 'org.eclipse.emf.mwe2.launch.runtime.Mwe2Launcher'
    classpath = sourceSets.main.runtimeClasspath
    args = ['src/main/java/gov/ca/dwr/wresl/xtext/editor/GenerateWreslEditor.mwe2']
    jvmArgs = ['-Xmx512m']
    ignoreExitValue = true
}

compileJava {
    options.failOnError = false
    doLast {
        if (compileJava.didWork && compileJava.state.failure != null) {
            logger.warn("Ignoring compile errors in compileJava task")
        }
    }
}

runMwe2.dependsOn classes
